import java.text.DateFormat
import java.text.SimpleDateFormat

def uuTrimAndQuote = { val ->
    return "\"" + val.trim() + "\""
}

def getBuildBranch = { ->

    def val = null

    try {
        //	git rev-parse --abbrev-ref HEAD
        def cmdLineOut = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--abbrev-ref", "HEAD"
            standardOutput = cmdLineOut
        }

        val = "$cmdLineOut"

    } catch (ex) {
        project.logger.lifecycle("Caught exception getting branch name from git: $ex")
    }

    if (val == null) {
        val = ""
    }

    return val
}

ext {
    BUILD_BRANCH = uuTrimAndQuote(getBuildBranch())
}

def getBuildCommitHash = { ->

    def val = null

    try {
        //	git rev-parse --abbrev-ref HEAD
        def cmdLineOut = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "HEAD"
            standardOutput = cmdLineOut
        }

        val = "$cmdLineOut"

    } catch (ex) {
        project.logger.lifecycle("Caught exception getting branch commit hash from git: $ex")
    }

    if (val == null) {
        val = ""
    }

    def result = uuTrimAndQuote(val)
    project.logger.lifecycle("buildCommitHash: $result")
    return result
}

def getBuildDate = { ->

    DateFormat df = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss ZZZ")
    def val = df.format(new Date())

	def result = "\"" + val.trim().replace("'", "") + "\""
    project.logger.lifecycle("buildDate: $result")
    return result
}

ext {
    PUBLISH_GROUP_ID = 'com.silverpine.uu'
    BUILD_VERSION = version
    BUILD_COMMIT_HASH = getBuildCommitHash()
    BUILD_DATE = getBuildDate()
}

group = PUBLISH_GROUP_ID

project.logger.lifecycle("BUILD_VERSION: $BUILD_VERSION")
project.logger.lifecycle("BUILD_BRANCH: $BUILD_BRANCH")

android {

    defaultConfig {

        buildConfigField "String", "BUILD_VERSION", uuTrimAndQuote(BUILD_VERSION)
        buildConfigField "String", "BUILD_BRANCH", BUILD_BRANCH
        buildConfigField "String", "BUILD_COMMIT_HASH", BUILD_COMMIT_HASH
        buildConfigField "String", "BUILD_DATE", BUILD_DATE
    }
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

android {
    publishing {
        singleVariant("release") {
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId uu_publish_artifact_id
                version BUILD_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                //artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = uu_publish_artifact_id
                    description = uu_publish_description
                    url = 'https://github.com/SilverpineSoftware/' + uu_scm_module_name
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/SilverpineSoftware/' + uu_scm_module_name + '/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'ryandevore'
                            name = 'Ryan DeVore'
                            email = 'ryan@silverpine.com'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/SilverpineSoftware/' + uu_scm_module_name + '.git'
                        developerConnection = 'scm:git:ssh://github.com/SilverpineSoftware/' + uu_scm_module_name + '.git'
                        url = 'https://github.com/SilverpineSoftware/' + uu_scm_module_name + '/tree/main'
                    }
                }
            }
        }
    }
}


android {
    compileSdkVersion uu_target_sdk

    defaultConfig {
        minSdkVersion uu_min_sdk
        targetSdkVersion uu_target_sdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility uu_java_version
        targetCompatibility uu_java_version
    }

    kotlinOptions {
        jvmTarget = uu_java_version
    }

    buildFeatures {
        buildConfig true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        managedDevices {
            localDevices {
                pixel1api27 {
                    device = "Pixel"
                    apiLevel = 27
                    systemImageSource = "aosp"
                }
                pixel2api28 {
                    device = "Pixel 2"
                    apiLevel = 28
                    systemImageSource = "aosp"
                }
                pixel3api29 {
                    device = "Pixel 3"
                    apiLevel = 29
                    systemImageSource = "aosp"
                }
                pixel4api30 {
                    device = "Pixel 4"
                    apiLevel = 30
                    systemImageSource = "aosp-atd"
                }
                pixel5api31 {
                    device = "Pixel 5"
                    apiLevel = 31
                    systemImageSource = "aosp-atd"
                }
                pixel6api32 {
                    device = "Pixel 6"
                    apiLevel = 32
                    systemImageSource = "aosp-atd"
                }
                pixel7api33 {
                    device = "Pixel 7"
                    apiLevel = 33
                    systemImageSource = "aosp-atd"
                }
                pixel8api34 {
                    device = "Pixel 8"
                    apiLevel = 34
                    systemImageSource = "aosp-atd"
                }
                pixel9api35 {
                    device = "Pixel 9"
                    apiLevel = 35
                    systemImageSource = "aosp-atd"
                }
                pixel10api36 {
                    device = "Pixel 10"
                    apiLevel = 36
                    systemImageSource = "aosp-atd"
                }
                nexus1api30 {
                    device = "Nexus One"
                    apiLevel = 30
                    systemImageSource = "aosp-atd"
                }
            }

            groups {
                // Middle of the road SDK for quick checks
                uuQuick {
                    targetDevices.add(localDevices.pixel7api33)
                }

                // Small range of tests for CI builds
                uuCi {
                    targetDevices.add(localDevices.pixel1api27)
                    targetDevices.add(localDevices.pixel4api30)
                    targetDevices.add(localDevices.pixel7api33)
                    targetDevices.add(localDevices.pixel9api35)
                }

                // Complete list of devices for full testing
                uuComplete {
                    targetDevices.add(localDevices.pixel1api27)
                    targetDevices.add(localDevices.pixel2api28)
                    targetDevices.add(localDevices.pixel3api29)
                    targetDevices.add(localDevices.pixel4api30)
                    targetDevices.add(localDevices.pixel5api31)
                    targetDevices.add(localDevices.pixel6api32)
                    targetDevices.add(localDevices.pixel7api33)
                    targetDevices.add(localDevices.pixel8api34)
                    targetDevices.add(localDevices.pixel9api35)
                    targetDevices.add(localDevices.nexus1api30)
                }
            }
        }
    }

    namespace uu_namespace
}
